plugins {
    id "java"
    id "jacoco"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.7.3"
}

repositories {
    jcenter()
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

test {
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

task sourcesJar(type: Jar, dependsOn: "classes") {
    from sourceSets.main.allJava
    classifier "sources"
}

task javadocJar(type: Jar, dependsOn: "javadoc") {
    from javadoc.destinationDir
    classifier "javadoc"
}

publishing {
    publications {
        UrlStringBuilder(MavenPublication) {
            groupId "com.trevorgowing"
            version "1.1.0"

            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")
    publications = ["UrlStringBuilder"]
    publish = false

    pkg {
        repo = "maven"
        name = "url-string-builder"
        licenses = ["MIT"]
        vcsUrl = "https://github.com/trevorgowing/url-string-builder.git"
        githubRepo = "trevorgowing/url-string-builder"

        version {
            name = "1.1.0"
            desc = "Added appendPath for int and long"
            released = new Date()
            vcsTag = "v1.1.0"
        }
    }
}